{
  inputs,
  flake-parts-lib,
  ...
}: {
  imports = [
    inputs.flake-parts.flakeModules.flakeModules
  ];
  flake.flakeModules.just = {
    options.perSystem = flake-parts-lib.mkPerSystemOption ({
      lib,
      pkgs,
      config,
      ...
    }: let
      inherit (lib) types mkOption;
      inherit (import ./utils.nix {inherit lib pkgs;}) recipeType;
    in {
      imports = [
        {
          options.snowblower.just.recipes = mkOption {
            type = types.submoduleWith {
              modules = [{freeformType = types.attrsOf recipeType;}];
              specialArgs = {inherit pkgs;};
            };
            default = {};
            description = ''
              The recipes that are avaliable to just
            '';
          };
        }
      ];

      options.snowblower.just = {
        package = mkOption {
          type = types.package;
          default = pkgs.just;
          defaultText = lib.literalExpression "pkgs.just";
          description = "The just package to use.";
        };

        commonFileName = mkOption {
          type = types.str;
          default = "just-flake.just";
          description = ''
            The name of the common justfile generated by this module.
          '';
        };
      };

      config.snowblower = {
        packages = [
          config.snowblower.just.package
        ];

        shell = {
          startup = let
            commonJustfile = pkgs.writeTextFile {
              name = "justfile";
              text =
                lib.concatStringsSep "\n"
                (lib.mapAttrsToList (_name: recipe: recipe.outputs.justfile) config.snowblower.just.recipes);
            };
          in [
            ''
              ln -sf ${builtins.toString commonJustfile} ./${config.snowblower.just.commonFileName}
            ''
          ];
        };
      };
    });
  };
}
